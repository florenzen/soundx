/**
 * Process judgement forms sections
 */
module org/sugarj/sxbld/processing/JudgementForms

imports org/sugarj/soundx/AbstractSyntax

imports org/sugarj/sxbld/Environment

imports org/sugarj/sxbld/processing/SortPrefixing
        org/sugarj/sxbld/processing/SDF2Terms

imports org/sugarj/languages/SXBld


/**
 * Register analyses and desugarings
 */
strategies
	sxbld-start-analysis = sxbld-analyze-judgement-forms

/**
 * Analysis
 */
strategies
	/**
	 * Analyzes all judgement forms and adds productions to the environment
	 *
	 * @type SXBldJudgementForms -> SXBldJudgementForms
	 */
	sxbld-analyze-judgement-forms =
		?SXBldJudgementForms(forms);
		where(
			<map(sxbld-analyze-judgement-form)> forms => prods;
			<sxbld-env-add-grammar-elems> [grammar-elem(context-free-syntax(prods))])

	/**
	 * Generates a production for SXJudgement
	 *
	 * Generates a constructor name for the judgement and
	 * prefixes all sort names.
	 *
	 * @type SXBldJudgementForm -> Sdf2Production
	 */
	sxbld-analyze-judgement-form =
		?SXBldJudgementForm(symbols);
	    <sxbld-env-fresh-judgement-name> () => judgement-name;
    	<sxbld-prefix-all-sorts-base-lang> symbols => prefixed-symbols;
    	!prod(prefixed-symbols, sort("SXJudgement"), attrs([<sxbld-sdf2-cons-attribute> judgement-name]))

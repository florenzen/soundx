/**
 * Process judgement forms sections
 */
module org/sugarj/sxbld/processing/JudgementForms

imports org/sugarj/soundx/AbstractSyntax

imports org/sugarj/sxbld/Environment

imports org/sugarj/sxbld/processing/SortRenaming
        org/sugarj/sxbld/processing/SDF2Terms

imports org/sugarj/languages/SXBld

strategies
	/**
	 * Translates a judgement form to a context-free grammar
	 *
	 * A production for the SXJudgement nonterminal is generated
	 * with the given cons-attribute.
	 *
	 * @type (List(Symbols), String) -> Sdf2Grammar
	 */
	sxbld-symbols-to-cf-syntax:
		(symbols, cons-name) -> context-free-syntax(production)
		where
			<sxbld-sdf2-cons-attribute> cons-name => cons-attribute;
			!prod(symbols, sort("SXJudgement"), attrs([cons-attribute])) => production

    /**
     * Extracts a form declaration from the symbols of a judgment form
     *
     * @type (List(Symbol), String) -> SXAbsJudgementForm
     */
    sxbld-extract-form-declaration =
    	?(symbols, name);
    	<mapconcat(\sort(sort-name) -> [sort-name]\ <+ ![])> symbols => sort-names;
        !SXAbsJudgementForm(sort-names, name)

	/**
	 * Processes a single judgement form
	 *
	 * @type SXBldJudgementForm -> HashTable(String, V)
	 */
    sxbld-process-judgement-form =
    	?SXBldJudgementForm(symbols);
    	<map(sxbld-rename-all-sorts)> symbols => renamed-symbols;
    	<sxbld-env-fresh-judgement-name> () => judgement-name;
    	<sxbld-extract-form-declaration> (renamed-symbols, judgement-name) => form-declaration;
    	<sxbld-env-add-form-declaration> form-declaration;
    	<sxbld-symbols-to-cf-syntax> (renamed-symbols, judgement-name) => grammar;
    	<sxbld-env-add-grammar-elems> [grammar-elem(grammar)]

    /**
     * Process judgement forms section
     *
     * Transforms judgement forms section to SDF2 and Stratego.
     * Stores the syntax and the form declaration in the environment.
     *
     * @type SXBldJudgementForms -> ToplevelDeclaration
     */
    sxbld-process-judgement-forms:
    	SXBldJudgementForms(judgement-forms) -> extension-decls
    	where
    		<map(sxbld-process-judgement-form)> judgement-forms;
    		sxbld-env-extract-extension-decls => extension-decls
/**
 * Process judgement forms sections
 */
module org/sugarj/sxbld/processing/JudgementForms

imports org/sugarj/soundx/AbstractSyntax

imports org/sugarj/sxbld/Environment

imports org/sugarj/sxbld/processing/SortRenaming
        org/sugarj/sxbld/processing/SDF2Terms
        org/sugarj/sxbld/processing/Symbols

imports org/sugarj/languages/SXBld

strategies
	/**
	 * Register analyses and desugarings
	 */
	sxbld-desugar-to-env = sxbld-desugar-judgement-forms
	sxbld-start-analysis = sxbld-analyze-judgement-forms

	sxbld-analyze-judgement-forms =
		SXBldJudgementForms(map(sxbld-analyze-judgement-form))
		
	sxbld-analyze-judgement-form =
		?form@SXBldJudgementForm(symbols);
		<sxbld-analyze-symbols-to-sorts> symbols => sort-names;
	    <sxbld-env-fresh-judgement-name> () => judgement-name;
    	<sxbld-env-add-form> (judgement-name, SXAbsJudgementForm(sort-names));
    	!(judgement-name, form)

	/**
	 * Desugars a single judgement form
	 *
	 * @type SXBldJudgementForm -> SXEnv
	 */
	sxbld-desugar-judgement-form =
		((?(judgement-name, SXBldJudgementForm(symbols));
		  <sxbld-sdf2-cons-attribute; !attrs(<id>)> judgement-name => attributes) <+
		 (?SXBldJudgementForm(symbols); // If analysis failed there is no cons name
		  !no-attrs() => attributes));
		<map(sxbld-rename-all-sorts)> symbols => renamed-symbols;
    	!prod(renamed-symbols, sort("SXJudgement"), attributes) => production;
    	<sxbld-env-add-grammar-elems> [grammar-elem(context-free-syntax(production))]
		

    /**
     * Desugars judgement forms section
     *
     * Transforms judgement forms section to SDF2 and Stratego.
     *
     * @type SXBldJudgementForms -> SXEnv
     */
    sxbld-desugar-judgement-forms =
    	SXBldJudgementForms(map(sxbld-desugar-judgement-form))

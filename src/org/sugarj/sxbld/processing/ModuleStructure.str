/**
 * Processing of declarations decsribing the module structure
 *
 * @author Florian Lorenzen <florian.lorenzen@tu-berlin.de>
 */
module org/sugarj/sxbld/processing/ModuleStructure

imports org/sugarj/sxbld/Environment
 	    org/sugarj/sxbld/processing/AnalysisFunctions

imports org/sugarj/soundx/AbstractSyntax

imports org/sugarj/languages/SXBld


/**
 * Register analyses
 */
strategies
	sxbld-start-analysis = sxbld-analyze-toplevel-dec
	sxbld-start-analysis = sxbld-analyze-toplevel-decs
	sxbld-start-analysis = sxbld-analyze-interface
	sxbld-start-analysis = sxbld-analyze-interface-judgement
	sxbld-start-analysis = sxbld-analyze-extension-begin
	sxbld-start-analysis = sxbld-analyze-extension-end

/**
 * Analyses
 */
strategies
	/**
	 * Stores sort of toplevel declaration in environment
	 *
	 * @type String -> String
	 */
	sxbld-analyze-toplevel-dec =
		?decl@SXBldToplevelDeclaration(sort-name);
		where(
			(sxbld-env-is-toplevel-declaration-defined;
			 <sxbld-analyze-fail(|"Only one `toplevel declaration' allowed")> decl) <+
			<sxbld-env-set-toplevel-declaration> sort-name)

    /**
     * Stores sort of list of toplevel declarations in environment
     *
     * @type String -> String
     */
    sxbld-analyze-toplevel-decs =
    	?decl@SXBldToplevelDeclarations(sort-name);
    	where(
    		(sxbld-env-is-toplevel-declarations-defined;
    		 <sxbld-analyze-fail(|"Only one `toplevel declarations' allowed")> decl) <+
    		<sxbld-env-set-toplevel-declarations> sort-name)

    /**
     * Stores sort of the module interface in the environment
     *
     * @type String -> String
     */
    sxbld-analyze-interface =
    	?decl@SXBldInterface(sort-name);
    	where(
    		(sxbld-env-is-interface-defined;
    		 <sxbld-analyze-fail(|"Only one `interface' allowed")> decl) <+
    		<sxbld-env-set-interface> sort-name)

    /**
     * Stores the interface judgement in the environment
     *
     * @type (SXBldIntfJudgToplevel, SXBldInfJudgInterface, SXJudgement) ->
     *   (SXBldIntfJudgToplevel, SXBldInfJudgInterface, SXJudgement)
     */
    sxbld-analyze-interface-judgement =
    	?decl@SXBldInterfaceJudgement(toplevel, intf, judg);
    	where(
    		(sxbld-env-is-interface-judgement-defined;
    		 <sxbld-analyze-fail(|"Only one `interface for - is - derived by -' allowed")> decl) <+
    		<sx-meta-var-to-sx-metavar; sxbld-env-set-interface-judgement> (toplevel, intf, judg))

    /**
     * Stores sort of extension begin
     *
     * @type String -> String
     */
    sxbld-analyze-extension-begin =
    	?decl@SXBldExtensionBegin(sort-name);
    	where(
    		(sxbld-env-is-extension-begin-defined;
    		 <sxbld-analyze-fail(|"Only one `extension begin' allowed")> decl) <+
    		<sxbld-env-set-extension-begin> sort-name)

    /**
     * Stores sort of extension end
     *
     * @type String -> String
     */
    sxbld-analyze-extension-end =
    	?decl@SXBldExtensionEnd(sort-name);
    	where(
    		(sxbld-env-is-extension-end-defined;
    		 <sxbld-analyze-fail(|"Only one `extension end' allowed")> decl) <+
    		<sxbld-env-set-extension-end> sort-name)

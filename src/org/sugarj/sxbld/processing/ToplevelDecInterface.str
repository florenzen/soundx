module org/sugarj/sxbld/processing/ToplevelDecInterface

imports org/sugarj/sxbld/Environment
 	    org/sugarj/sxbld/Analysis


imports org/sugarj/sxbld/processing/SDF2Terms
	    org/sugarj/soundx/AbstractSyntax

imports org/sugarj/languages/SXBld

strategies
	sxbld-start-analysis = sxbld-analyze-toplevel-dec
	sxbld-start-analysis = sxbld-analyze-toplevel-decs
	sxbld-start-analysis = sxbld-analyze-interface
	sxbld-start-analysis = sxbld-analyze-interface-judgement

strategies
	sxbld-analyze-toplevel-dec =
		?decl@SXBldToplevelDeclaration(sort-name);
		where(
			(sxbld-env-is-toplevel-declaration-defined;
			 <sxbld-analyze-fail(|"Only one `toplevel declaration' allowed")> decl) <+
			<sxbld-env-set-toplevel-declaration> sort-name)

    sxbld-analyze-toplevel-decs =
    	?decl@SXBldToplevelDeclarations(sort-name);
    	where(
    		(sxbld-env-is-toplevel-declarations-defined;
    		 <sxbld-analyze-fail(|"Only one `toplevel declarations' allowed")> decl) <+
    		<sxbld-env-set-toplevel-declarations> sort-name)

    sxbld-analyze-interface =
    	?decl@SXBldInterface(sort-name);
    	where(
    		(sxbld-env-is-interface-defined;
    		 <sxbld-analyze-fail(|"Only one `interface' allowed")> decl) <+
    		<sxbld-env-set-interface> sort-name)

    sxbld-analyze-interface-judgement =
    	?decl@SXBldInterfaceJudgement(toplevel, intf, judg);
    	where(
    		(sxbld-env-is-interface-judgement-defined;
    		 <sxbld-analyze-fail(|"Only one `interface for - is - derived by -' allowed")> decl) <+
    		<sxbld-meta-var-to-sx-metavar; sxbld-env-set-interface-judgement> (toplevel, intf, judg))

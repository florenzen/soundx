/**
 * Processing of SDF2 sections
 *
 * @author Florian Lorenzen <florian.lorenzen@tu-berlin.de>
 */
module org/sugarj/sxbld/processing/GrammarElems

imports org/sugarj/languages/SXBld

imports org/sugarj/soundx/AbstractSyntax

imports org/sugarj/sxbld/Environment
        org/sugarj/sxbld/Analysis

imports org/sugarj/sxbld/processing/SortRenaming
        org/sugarj/sxbld/processing/SDF2Terms

strategies
	/**
	 * Register analyses and desugarings
	 */
	sxbld-desugar-to-env = sxbld-desugar-grammar-elems
	sxbld-start-analysis = sxbld-analyze-grammar-elems

	/**
	 * Analyzes list of SDF2 sections
	 *
	 * @type SXBldGrammarElems -> SXBldGrammarElems
	 */
	sxbld-analyze-grammar-elems =
		?SXBldGrammarElems(grammars);
		<map(sxbld-analyze-grammar-elem)> grammars => analyzed-grammars;
		!SXBldGrammarElems(analyzed-grammars)

    /**
     * Analyzes a single SDF2 section
     *
     * @type Grammar -> Grammar
     */
    sxbld-analyze-grammar-elem =
        (sxbld-analyze-sorts +
         sxbld-analyze-cf-syntax +
         sxbld-analyze-lex-syntax +
         sxbld-analyze-variables +
         sxbld-analyze-lex-priorities +
         sxbld-analyze-cf-priorities +
         sxbld-analyze-lex-restrictions +
         sxbld-analyze-cf-restrictions) <+
        sxbld-analyze-unsupported(|"Unsupported SDF 2 section")

	/**
	 * Passes on lexical priorities untouched
	 *
	 * @type Grammar -> Grammar
	 */
	sxbld-analyze-lex-priorities =
		?lexical-priorities(_)
	
   /**
	 * Passes on context-free priorities untouched
	 *
	 * @type Grammar -> Grammar
	 */
	sxbld-analyze-cf-priorities =
		?context-free-priorities(_)

    /**
	 * Passes on lexical restrictions untouched
	 *
	 * @type Grammar -> Grammar
	 */
	sxbld-analyze-lex-restrictions =
		?lexical-restrictions(_)

	/**
	 * Passes on context-free priorities untouched
	 *
	 * @type Grammar -> Grammar
	 */
	sxbld-analyze-cf-restrictions =
		?context-free-restrictions(_)

	/**
	 * Analyzes a variables section
	 *
	 * Transforms it into a context-free section
	 *
	 * @type Grammar -> Grammar
	 */
	sxbld-analyze-variables =
		?variables(prods);
		<map(sxbld-analyze-variable-prod)> prods => analyzed-prods;
		!context-free-syntax(analyzed-prods)

	/**
	 * Analyzes a variable production
	 *
	 * @type Sdf2Production -> Sdf2Production
	 */
	sxbld-analyze-variable-prod =
		?prod(lhs-symbols, rhs-symbol, attributes);
		<sxbld-analyze-variable-prod-rhs> rhs-symbol => analyzed-rhs-symbol;
		<sxbld-analyze-variable-prod-attributes(|analyzed-rhs-symbol)> attributes => analyzed-attributes;
		!prod(lhs-symbols, analyzed-rhs-symbol, analyzed-attributes)

    /**
     * Checks that right-hand side is of supported format
     *
     * This aligns with the supported context-free and
     * lexical reight-hand sides.
     *
     * @type Sdf2Symbol -> Sdf2Symbol
     */
    sxbld-analyze-variable-prod-rhs =
	    ?sort(_) <+
    	sxbld-analyze-unsupported(|"Unsupported right-hand side")

	sxbld-analyze-variable-prod-attributes(|rhs-symbol) =
		?no-attrs();
		!attrs([prefer(), <sxbld-analyze-variable-cons> rhs-symbol])
	sxbld-analyze-variable-prod-attributes(|rhs-symbol) =
		?attrs(_);
		sxbld-analyze-unsupported(|"Attributes not supported in variables section")

    sxbld-analyze-variable-cons:
    	sort(name) -> cons-attribute
    	where
    		<sxbld-env-get-base-language-name> () => base-language-name;
    		<conc-strings> (base-language-name, "MetaVar", name) => cons-name;
    		<sxbld-sdf2-cons-attribute> cons-name => cons-attribute

    /**
     * Analyzes a sorts section
     *
     * @type Grammar -> Grammar
     */
    sxbld-analyze-sorts =
        ?'sorts(symbols);
    	<map(sxbld-analyze-symbol)> symbols => analyzed-symbols;
   	    !'sorts(analyzed-symbols)

    /**
     * Analyzes one symbol of a sorts section
     *
     * The sort name is added as SXAbsUnknownSort() to the environment
     *
     * @type Sdf2Symbol -> Sdf2Symbol
     */
    sxbld-analyze-symbol =
    	sxbld-analyze-symbol-sort <+
    	sxbld-analyze-unsupported(|"Unsupported sort declaration")

    /**
     * Analyzes a sort name
     *
     * The sort name is added as SXAbsUnknownSort() to the environment
     *
     * @type Sdf2Symbol -> Sdf2Symbol
     */
	sxbld-analyze-symbol-sort =
		?symbol@sort(name);
		<sxbld-add-sort> (name, SXAbsSortUnknown());
		!symbol

    /**
     * Succeeds if two sorts are equal, fails otherwise
     *
     * SXAbsSortUnknown() is equal to any sort.
     *
     * @type (SXAbsSort, SXAbsSort) -> (SXAbsSort, SXAbsSort)
     */
    sxbld-sorts-equal =
   		?(SXAbsSortUnknown(), _) +
   		?(_, SXAbsSortUnknown()) +
   		?(SXAbsSortLexical(), SXAbsSortLexical()) +
   		?(SXAbsSortContextFree(), SXAbsSortContextFree())

    /**
     * Adds a sort to the environment
     *
     * If a sort of the same name is already defined the two
     * sorts must be equal, otherwise the analysis fails.
     * If the previously defined sort is
     * SXAbsSortUnknown() it is updated by the new sort.
     *
     * @type (String, SXAbsSort) -> SXEnv
     */
    sxbld-add-sort =
    	?(name, sort);
    	<sxbld-env-get-sort> name;
    	if ?None() then
    		<sxbld-env-add-sort> (name, sort)
    	else
    		?Some(other-sort);
    		if <sxbld-sorts-equal> (sort, other-sort) then
    			<sxbld-add-sort-unless-unknown> (name, sort)
    		else
    			<sxbld-analyze-fail(|"Invalid redeclaration of sort")> name
    		end
    	end

    /**
      * Adds a sort to the environment only if its not SXAbsSortUnknown()
      *
      * @type (String, SXAbsSort) -> SXEnv
      */
    sxbld-add-sort-unless-unknown =
    	(?(name, SXAbsSortUnknown()); sxbld-get-env-hashtable) <+ // Return the environment in any case
    	(?(name, sort); sxbld-env-add-sort)

    /**
     * Analyzes a context-free syntax section
     *
     * @type Grammar -> Gramar
     */
    sxbld-analyze-cf-syntax =
    	?context-free-syntax(prods);
    	<map(sxbld-analyze-cf-production)> prods => analyzed-prods;
        !context-free-syntax(analyzed-prods)

    /**
     * Analyzes a context-free production
     *
     * @type Sdf2Production -> Sdf2Production
     */
    sxbld-analyze-cf-production =
    	?prod(symbols, symbol, attributes);
    	<sxbld-analyze-cf-rhs> symbol;
    	!prod(symbols, symbol, attributes)

    /**
     * Analyzes the right-hand side of a context-free production
     *
     * @type Sdf2Symbol -> Sdf2Symbol
     */
    sxbld-analyze-cf-rhs =
    	sxbld-analyze-cf-rhs-sort <+
    	sxbld-analyze-unsupported(|"Unsupported right-hand side")

    /**
     * Adds the right-hand side sort as SXAbsSortContextFree() to environment
     *
     * @type Symbol -> SXEnv
     */
    sxbld-analyze-cf-rhs-sort =
    	?sort(name);
    	<sxbld-add-sort> (name, SXAbsSortContextFree())

    /**
     * Analyzes a lexical syntax section
     *
     * @type Grammar -> Gramar
     */
    sxbld-analyze-lex-syntax =
    	?lexical-syntax(prods);
    	<map(sxbld-analyze-lex-production)> prods => analyzed-prods;
        !lexical-syntax(analyzed-prods)

    /**
     * Analyzes a lexical production
     *
     * @type Sdf2Production -> Sdf2Production
     */
    sxbld-analyze-lex-production =
    	?prod(symbols, symbol, attributes);
    	<sxbld-analyze-lex-rhs> symbol;
    	!prod(symbols, symbol, attributes)

    /**
     * Analyzes the right-hand side of a lexical production
     *
     * @type Sdf2Symbol -> Sdf2Symbol
     */
    sxbld-analyze-lex-rhs =
    	sxbld-analyze-lex-rhs-sort <+
    	sxbld-analyze-unsupported(|"Unsupported right-hand side")

    /**
     * Adds the right-hand side sort as SXAbsSortLexical() to environment
     *
     * @type Symbol -> SXEnv
     */
    sxbld-analyze-lex-rhs-sort =
    	?sort(name);
    	<sxbld-add-sort> (name, SXAbsSortLexical())
   
    /**
     * Renames all sorts in the grammars and adds them to the environment
     *
     * @type SXBldGrammarElems -> SXEnv
     */   
    sxbld-desugar-grammar-elems =
    	?SXBldGrammarElems(grammars);
    	<sxbld-rename-all-sorts> grammars => renamed-grammars;
    	<map(\grm -> grammar-elem(grm)\)> renamed-grammars => grammar-elems;
    	<sxbld-env-add-grammar-elems> grammar-elems

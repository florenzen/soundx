/**
 * Processing of SDF2 sections
 *
 * @author Florian Lorenzen <florian.lorenzen@tu-berlin.de>
 */
module org/sugarj/sxbld/processing/GrammarElems

imports org/sugarj/languages/SXBld

imports org/sugarj/soundx/AbstractSyntax

imports org/sugarj/sxbld/Environment
        org/sugarj/sxbld/Analysis

imports org/sugarj/sxbld/processing/SortRenaming

strategies
	/**
	 * Register analyses and desugarings
	 */
	sxbld-desugar-to-env = sxbld-desugar-grammar-elems
	sxbld-start-analysis = sxbld-analyze-grammar-elems

	/**
	 * Analyzes list of SDF2 sections
	 *
	 * @type SXBldGrammarElems -> SXBldGrammarElems
	 */
	sxbld-analyze-grammar-elems =
		?SXBldGrammarElems(grammars);
		<map(sxbld-analyze-grammar-elem)> grammars => analyzed-grammars;
		!SXBldGrammarElems(analyzed-grammars)

    /**
     * Analyzes a single SDF2 section
     *
     * @type Grammar -> Grammar
     */
    sxbld-analyze-grammar-elem =
        sxbld-analyze-sorts

    /**
     * Analyzes a sorts section
     *
     * @type Grammar -> Grammar
     */
    sxbld-analyze-sorts =
        ?'sorts(symbols);
    	<map(sxbld-analyze-symbol)> symbols => analyzed-symbols;
   	    !'sorts(analyzed-symbols)
   	    //sxbld-analyze-fail(|"Unsupported SDF2 section `sorts'")	   	    

    /**
     * Analyzes one symbol of a sorts section
     *
     * The sort name is added as SXAbsUnknownSort() to the environment
     *
     * @type Symbol -> SXEnv
     */
    sxbld-analyze-symbol =
    	sxbld-analyze-symbol-sort <+
    	sxbld-analyze-fail(|"Unsupported sort declaration")

    /**
     * Analyzes a sort name
     *
     * The sort name is added as SXAbsUnknownSort() to the environment
     *
     * @type Sdf2Symbol -> SXEnv
     */
	sxbld-analyze-symbol-sort =
		?sort(name);
		<sxbld-add-sort> (name, SXAbsSortUnknown())

    /**
     * Succeeds if two sorts are equal, fails otherwise
     *
     * SXAbsSortUnknown() is equal to any sort.
     *
     * @type (SXAbsSort, SXAbsSort) -> (SXAbsSort, SXAbsSort)
     */
    sxbld-sorts-equal =
   		?(SXAbsSortUnknown(), _) +
   		?(_, SXAbsSortUnknown()) +
   		?(SXAbsSortLexical(), SXAbsSortLexical()) +
   		?(SXAbsSortContextFree(), SXAbsSortContextFree())

    /**
     * Adds a sort to the environment
     *
     * If a sort of the same name is already defined the two
     * sorts must be equal, otherwise the analysis fails.
     * If the previously defined sort is
     * SXAbsSortUnknown() it is updated by the new sort.
     *
     * @type (String, SXAbsSort) -> SXEnv
     */
    sxbld-add-sort =
    	?(name, sort);
    	<sxbld-env-get-sort> name;
    	if ?None() then
    		<sxbld-env-add-sort> (name, sort)
    	else
    		?Some(other-sort);
    		if <sxbld-sorts-equal> (sort, other-sort) then
    			<sxbld-add-sort-unless-unknown> (name, sort)
    		else
    			<sxbld-analyze-fail(|"Invalid redeclaration of sort")> name
    		end
    	end

    /**
      * Adds a sort to the environment only if its not SXAbsSortUnknown()
      *
      * @type (String, SXAbsSort) -> SXEnv
      */
    sxbld-add-sort-unless-unknown =
    	(?(name, SXAbsSortUnknown()); sxbld-get-env-hashtable) <+ // Return the environment in any case
    	(?(name, sort); sxbld-env-add-sort)
    	
    /**
     * Renames all sorts in the grammars and adds them to the environment
     *
     * @type SXBldGrammarElems -> SXEnv
     */   
    sxbld-desugar-grammar-elems =
    	?SXBldGrammarElems(grammars);
    	<sxbld-rename-all-sorts> grammars => renamed-grammars;
    	<map(\grm -> grammar-elem(grm)\)> renamed-grammars => grammar-elems;
    	<sxbld-env-add-grammar-elems> grammar-elems

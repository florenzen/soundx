/*
 * Copyright (c) 2015, TU Berlin
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are
 * met:
 *
 * - Redistributions of source code must retain the above copyright
 *   notice, this list of conditions and the following disclaimer.
 * - Redistributions in binary form must reproduce the above
 *   copyright notice, this list of conditions and the following
 *   disclaimer in the documentation and/or other materials provided
 *   with the distribution.
 * - Neither the name of the TU Berlin nor the names of its
 *   contributors may be used to endorse or promote products derived
 *   from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

/**
 * Processing of SDF2 sections
 *
 * @author Florian Lorenzen <florian.lorenzen@tu-berlin.de>
 */
module org/sugarj/sxbld/processing/GrammarElems

imports org/sugarj/languages/SXBld

imports org/sugarj/soundx/AbstractSyntax
		org/sugarj/soundx/SDF2

imports org/sugarj/sxbld/Environment
        org/sugarj/sxbld/processing/AnalysisFunctions

imports org/sugarj/sxbld/processing/SortPrefixing

imports org/sugarj/soundx/Debug


/**
 * Register analyses
 */
strategies
	sxbld-start-analysis = sxbld-analyze-grammar-elems

/**
 * Analyses
 */
strategies
	/**
	 * Analyzes list of SDF2 sections
	 *
	 * Adds SDF2 sections as grammar elems to the environment
	 *
	 * @type SXBldGrammarElems -> SXBldGrammarElems
	 */
	sxbld-analyze-grammar-elems =
		?SXBldGrammarElems(grammars);
		where(
			<map(sxbld-analyze-grammar-elem); concat> grammars;
		    // Not using mapconcat here because it traverses the list from the end to front
		    // which lowers quality of error messages
			sxbld-add-grammar-elems)

    /**
     * Analyzes and transforms a single SDF2 section
     *
     * Gives an error on an unsupported section
     *
     * @type Grammar -> List(Grammar)
     */
    sxbld-analyze-grammar-elem =
        ((sxbld-analyze-sorts +
          sxbld-analyze-cf-syntax +
          sxbld-analyze-lex-syntax +
          sxbld-analyze-variables +
          sxbld-analyze-lex-priorities +
          sxbld-analyze-cf-priorities +
          sxbld-analyze-lex-restrictions +
          sxbld-analyze-cf-restrictions) <+
         sxbld-analyze-unsupported(|"Unsupported SDF 2 section"));
        (?[_|_] <+ ![<id>])

	/**
	 * Prefixes sorts in lexical priorities
	 *
	 * @type Grammar -> Grammar
	 */
	sxbld-analyze-lex-priorities =
		?lexical-priorities(prios);
		<sxbld-prefix-all-sorts(|"Gnd")> prios => prios-gnd;
		<conc; sxbld-prefix-all-sorts-base-lang> (prios, prios-gnd) => prios-prefixed;
		!lexical-priorities(prios-prefixed)
	
   /**
	 * Prefixes sorts in context-free priorities
	 *
	 * @type Grammar -> Grammar
	 */
	sxbld-analyze-cf-priorities =
		?context-free-priorities(prios);
		<sxbld-prefix-all-sorts(|"Gnd")> prios => prios-gnd;
		<conc; sxbld-prefix-all-sorts-base-lang> (prios, prios-gnd) => prios-prefixed;
		!context-free-priorities(prios-prefixed)

    /**
	 * Prefixes sorts in lexical restrictions
	 *
	 * @type Grammar -> Grammar
	 */
	sxbld-analyze-lex-restrictions =
		?lexical-restrictions(restr);
	    <sxbld-prefix-all-sorts(|"Gnd")> restr => restr-gnd;
		<conc; sxbld-prefix-all-sorts-base-lang> (restr, restr-gnd) => restr-prefixed;
	    !lexical-restrictions(restr-prefixed)

	/**
	 * Prefixes sorts in context-free restrictions
	 *
	 * @type Grammar -> Grammar
	 */
	sxbld-analyze-cf-restrictions =
		context-free-restrictions(sxbld-prefix-all-sorts-base-lang)

	/**
	 * Prefixes sorts in variables
	 *
	 * @type Grammar -> Grammar
	 */
	sxbld-analyze-variables =
	    variables(sxbld-prefix-all-sorts-base-lang)

    /**
     * Prefixes sorts in sorts
     *
     * @type Grammar -> Grammar
     */
    sxbld-analyze-sorts =
        'sorts(sxbld-prefix-all-sorts-base-lang)
    	// <map(sxbld-analyze-symbol); sxbld-prefix-all-sorts-base-lang> symbols => analyzed-symbols;
   	 //    !'sorts(analyzed-symbols)

    /**
     * Analyzes a context-free syntax section
     *
     * @type Grammar -> Grammar
     */
    sxbld-analyze-cf-syntax =
    	?context-free-syntax(prods);
    	<map(sxbld-analyze-cf-production); concat> prods => analyzed-prods;
        !context-free-syntax(analyzed-prods)

    /**
     * Analyzes a context-free production
     *
     * @type Sdf2Production -> List(Sdf2Production)
     */
    sxbld-analyze-cf-production =
    	?prod(symbols, symbol, attributes);
    	<sxbld-analyze-cf-attributes> attributes => analyzed-attributes;
    	<sxbld-analyze-body-dec> (symbol, analyzed-attributes);
    	<sxbld-analyze-namespace-dec> (symbols, symbol, analyzed-attributes);
    	<sxbld-analyze-import-dec> (symbols, symbol, analyzed-attributes);
    	<sxbld-prefix-all-sorts-base-lang> symbol => prefixed-symbol;
    	<sxbld-prefix-all-sorts-base-lang> symbols => prefixed-symbols;
    	<sxbld-prefix-all-sorts(|"Gnd"); sxbld-prefix-all-sorts-base-lang> symbols => prefixed-ground-symbols;
    	<sxbld-prefix-all-sorts(|"Gnd"); sxbld-prefix-all-sorts-base-lang> symbol => prefixed-ground-symbol;
    	<sxbld-analyze-universal-desugaring-prod> prefixed-symbol => univ-prod;
    	![univ-prod,
    	  prod(prefixed-symbols, prefixed-symbol, analyzed-attributes),
    	  prod(prefixed-ground-symbols, prefixed-ground-symbol, analyzed-attributes)]

	/**
	 * Generates a production for a universal desugaring
	 *
	 * @type Sdf2Symbol -> Sdf2Production
	 */
	sxbld-analyze-universal-desugaring-prod =
		// This simple approach generates the production many times for
		// a single sort blowing up the SDF2 code.
		?symbol;
		!prod([lit("{"), symbol, sort("SXDESUGARINGSEP"), symbol, lit("}")],
			sort("SXDesugaring"),
			attrs([<sx-sdf2-cons-attribute> "SXUniversalDesugaring"]))

    /**
     * Checks attributes of context-free production
     *
     * Generates a constructor name if necessary, includes it in
     * the analyzed attributes and also returns it.
     *
     * @param sort-name String name of the rhs sort to include in
     *   constructor name
     *
     * @type Sdf2Attributes -> Sdf2Attributes
     */
    sxbld-analyze-cf-attributes =
    	((?no-attrs(); ![]) +
    	 (?attrs(attrib-list); !attrib-list));
    	sxbld-analyze-cf-attribute-list-cons => attrib-list-with-cons;
    	!attrs(attrib-list-with-cons)

    /**
     * Adds or prefixes a cons attribute
     *
     * It returns the new attribute list and the name of the constructor.
     * If a constructor name is given in an attribut the exact string from
     * the input is returned. Therefore, the constructor name is double-qouted.
     *
     * A constructor is only returned if the attribute list does not contain a
     * bracket attribute.
     *
     * @param sort-name String name of the rhs sort to include in
     *   constructor name
     * @type List(Sdf2Attribute) -> List(Sdf2Attribute)
     */
    sxbld-analyze-cf-attribute-list-cons =
    	where(<elem> (bracket(), <id>)) <
    	// bracket attribute, return attributes untouched
    	id +
    	// No bracket attribute
    	(partition(sx-sdf2-is-attribute(|"cons")) => (cons-attribs, other-attribs);
    	 ((// No cons attribute, generate one
    	   !cons-attribs; ?[];
     	   sxbld-env-fresh-cons-name => cons-name;
		   sxbld-env-add-cons-name;
    	   ![<sx-sdf2-cons-attribute> cons-name | other-attribs]) <+
    	  (// One cons attribute, prefix it
    	   !cons-attribs; ?[cons-attrib];
    	   <sx-sdf2-cons-attribute-name-quoted> cons-attrib => cons-name-quoted;
    	   <un-double-quote> cons-name-quoted => cons-name;
    	   <sxbld-env-prefix-name> cons-name => prefixed-cons-name;
		   if sxbld-env-cons-name-defined then
		   	   <sxbld-analyze-fail(|"Duplicate constructor name")> cons-name-quoted
		   else
		   	   <sxbld-env-add-cons-name> prefixed-cons-name;
    	       ![<sx-sdf2-cons-attribute> prefixed-cons-name | other-attribs]
    	   end) <+
    	  (// More than one cons attribute, error
    	   !cons-attribs; ?[_,_|_];
    	   <map(sxbld-analyze-fail(|"Several cons attributes are not allowed"))> cons-attribs)))

	/**
	 * Registers constructor and sort of body declaration if sx-body-dec attribute is given
	 *
	 * The attributes must contain a cons attribute.
	 *
	 * @type (Sdf2Symbol, Sdf2Attributes) -> (Sdf2Symbol, Sdf2Attributes)
	 */
	sxbld-analyze-body-dec =
		?(symbol, attributes);
		where(
			if <sx-sdf2-is-aterm-attribute-defined(|"sx-body-dec")> attributes then
				<sx-sdf2-extract-aterm-attribute(|"cons")> attributes => cons-attrib;
				<sx-sdf2-cons-attribute-name-quoted; un-double-quote> cons-attrib => cons-name;
				//<sxbld-prefix-all-sorts(|"Gnd"); sxbld-prefix-all-sorts-base-lang> symbol => prefixed-symbol;
				<sxbld-env-add-body-dec> (cons-name, symbol)
			else
				id
			end)

	/**
	 * Registers constructor and sort of namespace declaration if sx-namespace-dec is given
	 *
	 * The attributes must contain a cons attribute.
	 *
	 * @type (Sdf2Symbols, Sdf2Symbol, Sdf2Attributes) -> (Sdf2Symbols, Sdf2Symbol, Sdf2Attributes)
	 */
	sxbld-analyze-namespace-dec =
		?(symbols, symbol, attributes);
		where(
			if <sx-sdf2-is-aterm-attribute-defined(|"sx-namespace-dec")> attributes then
				<sx-sdf2-extract-aterm-attribute(|"sx-namespace-dec")> attributes => namespace-attrib;
				if sxbld-env-is-namespace-dec-defined then
					<sxbld-analyze-fail(|"Only one `sx-namespace-dec' allowed")> namespace-attrib
				else
					<sx-sdf2-extract-aterm-attribute(|"cons")> attributes => cons-attrib;
					<sx-sdf2-cons-attribute-name-quoted; un-double-quote> cons-attrib => cons-name;
					<sx-sdf2-get-args-from-aterm-attribute> namespace-attrib => namespace-args;
					if ?[fun(unquoted(label))] then
						if <sx-sdf2-find-index-of-labelled-symbol(|label)> symbols => index then
							//<sxbld-prefix-all-sorts(|"Gnd"); sxbld-prefix-all-sorts-base-lang> symbol => prefixed-symbol;
							<sxbld-env-set-namespace-dec> (cons-name, index, symbol)
						else
							<sxbld-analyze-fail(|"Label of `sx-namespace-dec' must be attached to symbol on lhs")> label
						end
					else
						<sxbld-analyze-fail(|"`sx-namespace-dec' takes exatcly one argument")> namespace-attrib
					end
				end
			else
				id
			end)

	/**
	 * Registers constructor and sort of import declaration if sx-import-dec is given
	 *
	 * The attributes must contain a cons attribute.
	 *
	 * @type (Sdf2Symbols, Sdf2Symbol, Sdf2Attributes) -> (Sdf2Symbols, Sdf2Symbol, Sdf2Attributes)
	 */
	sxbld-analyze-import-dec =
		?(symbols, symbol, attributes);
		where(
			if <sx-sdf2-is-aterm-attribute-defined(|"sx-import-dec")> attributes then
				<sx-sdf2-extract-aterm-attribute(|"sx-import-dec")> attributes => import-attrib;
				<sx-sdf2-extract-aterm-attribute(|"cons")> attributes => cons-attrib;
				<sx-sdf2-cons-attribute-name-quoted; un-double-quote> cons-attrib => cons-name;
				<sx-sdf2-get-args-from-aterm-attribute> import-attrib => import-args;
				if ?[fun(unquoted(label))] then
					if <sx-sdf2-find-index-of-labelled-symbol(|label)> symbols => index then
						//<sxbld-prefix-all-sorts(|"Gnd"); sxbld-prefix-all-sorts-base-lang> symbol => prefixed-symbol;
						<sxbld-env-add-import-dec> (cons-name, index, symbol)
					else
						<sxbld-analyze-fail(|"Label of `sx-import-dec' must be attached to symbol on lhs")> label
					end
				else
					<sxbld-analyze-fail(|"`sx-import-dec' takes exatcly one argument")> import-attrib
				end
			else
				id
			end)

    /**
     * Analyzes a lexical syntax section
â€“     *
     * @type Grammar -> Gramar
     */
    sxbld-analyze-lex-syntax =
    	?lexical-syntax(prods);
    	<map(sxbld-analyze-lex-production); unzip; (concat, concat)> prods => (lex-prods, cf-prods);
        ![lexical-syntax(lex-prods), context-free-syntax(cf-prods)]

    /**
     * Analyzes a lexical production
     *
     * @type Sdf2Production -> (List(Sdf2Production), List(Sdf2Production))
     */
    sxbld-analyze-lex-production =
    	?prod(symbols, symbol, attributes);
    	<sxbld-prefix-all-sorts-base-lang> [symbol|symbols] => [prefixed-symbol|prefixed-symbols];
    	<sxbld-prefix-all-sorts(|"Gnd"); sxbld-prefix-all-sorts-base-lang> symbols => ground-symbols;
    	<sxbld-prefix-all-sorts(|"Gnd"); sxbld-prefix-all-sorts-base-lang> symbol => ground-symbol;
    	<sxbld-analyze-atoms> (attributes, prefixed-symbol) => cf-prods;
    	<sxbld-analyze-namespace-kind> (attributes, prefixed-symbol);
    	!([prod(prefixed-symbols, prefixed-symbol, attributes),
    	   prod(ground-symbols, ground-symbol, attributes)],
    	  cf-prods)

    /**
     * Generates inequality judgement for symbol if sx-requires-inequality is given
     * Generates "fresh from" and inequality judgement and syntax for atom sets.
     *
     * @type (Sdf2Attributes, Sdf2Symbol) -> List(Sdf2Production)
     */
    sxbld-analyze-atoms =
    	?(attributes, symbol);
    	if <sx-sdf2-extract-aterm-attribute(|"sx-atoms")> attributes => atoms-attrib then
    		if !symbol; ?sort(rhs-name) then
    			// <sx-sdf2-get-arg-from-aterm-attribute(|1)> atoms-attrib => atom-set-sort;
    			// if !atom-set-sort; ?fun(unquoted(sort-name)); <is-string> sort-name then
    				<sxbld-env-add-atom-sort> rhs-name;
    				// <sxbld-prefix-string-base-lang> sort-name => prefixed-sort-name;
    				![prod([symbol, lit("=/="), symbol], sort("SXJudgement"),
    	        		attrs([<sx-sdf2-cons-attribute> "SXNeq"]))]
    	        // else
    			//	<sxbld-analyze-fail(|"invalid argument to `sx-atoms'")> atom-set-sort
    			// end
    		else
    			<sxbld-analyze-fail(|"`sx-atoms' requires a sort as right-hand side")> symbol
    		end
    	else
    		![]
    	end
    		//<sx-debug(|"Found the following atom set symbol ")> atom-set-symbol;
    		
    // 		if !atom-set-sort; ?fun(unquoted(sort-name)); <is-string> sort-name then
    // 			<sxbld-env-add-atom-sort> rhs-name;
    // 			<sxbld-prefix-string-base-lang> sort-name => prefixed-sort-name;
    // 			![prod([symbol, lit("=/="), symbol], sort("SXJudgement"),
    // 	        	attrs([<sx-sdf2-cons-attribute> "SXNeq"]))] => grms-neq;
    // 	        ![prod([symbol, lit("notin"), sort(prefixed-sort-name)], sort("SXJudgement"),
    // 	        	attrs([<sx-sdf2-cons-attribute> "SXFreshIn"])),
    // 	          prod([symbol, lit("in"), sort(prefixed-sort-name)], sort("SXJudgement"),
    // 	          	attrs([<sx-sdf2-cons-attribute> "SXContainedIn"]))] => grms-fresh-in;
    // 	        // AtomSet "U" AtomSet  -> AtomSet {cons("SXAtomsUnion"), left}
    // 	        // "{" SXAtomsEnum "}"  -> AtomSet {cons("SXAtomsEnum")}
    // 	        // AtomSet "\\" AtomSet -> AtomSet {cons("SXAtomsDiff"), left}
    // 	        // "(" AtomSet ")"      -> AtomSet {bracket}
    // 	        //
    // 	        //                      -> SXAtomsEnum {cons("SXAtomsEnumNone")}
    // 	        // Atom SXAtomsList     -> SXAtomsEnum {cons("SXAtomsEnumOne")}
    // 	        //                      -> SXAtomsList {cons("SXAtomsListNil")}
    // 	        // "," Atom SXAtomsList -> SXAtomsList {cons("SXAtomsListCons")}
    // 	        // TODO: precedences, associativity, parentheses
    // 	        !sort(prefixed-sort-name) => atom-set;
    // 	        !symbol => atom;
    // 	        ![prod([atom-set, lit("U"), atom-set], atom-set,
    // 	        	attrs([<sx-sdf2-cons-attribute> "SXAtomsUnion", assoc(left())])),
    // 	          prod([lit("{"), sort("SXAtomsEnum"), lit("}")], atom-set,
    // 	        	attrs([<sx-sdf2-cons-attribute> "SXAtomsEnum"])),
    // 	          prod([atom-set, lit("\\\\"), atom-set], atom-set,
    // 	        	attrs([<sx-sdf2-cons-attribute> "SXAtomsDiff", assoc(left())])),
    // 	          prod([lit("("), atom-set, lit(")")], atom-set,
    // 	          	attrs([bracket()])),
    // 	          prod([], sort("SXAtomsEnum"),
    // 	        	attrs([<sx-sdf2-cons-attribute> "SXAtomsEnumNone"])),
    // 	          prod([atom, sort("SXAtomsList")], sort("SXAtomsEnum"),
    // 	        	attrs([<sx-sdf2-cons-attribute> "SXAtomsEnumOne"])),
    // 	          prod([], sort("SXAtomsList"),
    // 	            attrs([<sx-sdf2-cons-attribute> "SXAtomsListNil"])),
    // 	          prod([lit(","), atom, sort("SXAtomsList")], sort("SXAtomsList"),
    // 	            attrs([<sx-sdf2-cons-attribute> "SXAtomsListCons"]))
    // 	         ] => grms-atoms;
				// <conc> (grms-neq, grms-fresh-in, grms-atoms)

    	    
    /**
     * Records the module identifier sort and namespace kind if sx-namespace-flat is given
     *
     * @type (Sdf2Attributes, Sdf2Symbol) -> (Sdf2Attributes, Sdf2Symbol)
     */
    sxbld-analyze-namespace-kind =
    	?(attributes, symbol);
    	where(
    		if <sx-sdf2-is-aterm-attribute-defined(|"sx-namespace-flat")> attributes then
    			if sxbld-env-is-namespace-kind-defined then
    				<sx-sdf2-extract-aterm-attribute(|"sx-namespace-flat")> attributes => attrib;
    				<sxbld-analyze-fail(|"Only one `sx-namespace' attribute allowed")> attrib
    			else
    				<sxbld-env-set-namespace-kind> SXBldNamespaceFlat();
    				<sxbld-env-set-namespace-identifier> symbol
    			end
    		else
				id
    		end)

    /**
     * Adds the grammars to the environment
     *
     * @type SXBldGrammarElems -> SXEnv
     */   
   sxbld-add-grammar-elems =
   	   map(\grm -> grammar-elem(grm)\);
   	   sxbld-env-add-grammar-elems

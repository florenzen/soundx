/*
 * Copyright (c) 2015, TU Berlin
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are
 * met:
 *
 * - Redistributions of source code must retain the above copyright
 *   notice, this list of conditions and the following disclaimer.
 * - Redistributions in binary form must reproduce the above
 *   copyright notice, this list of conditions and the following
 *   disclaimer in the documentation and/or other materials provided
 *   with the distribution.
 * - Neither the name of the TU Berlin nor the names of its
 *   contributors may be used to endorse or promote products derived
 *   from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

/**
 * Prefixing of SDF2 sort names.
 *
 * @author Florian Lorenzen <florian.lorenzen@tu-berlin.de>
 */
module org/sugarj/sxbld/processing/SortPrefixing

imports org/sugarj/languages/Sdf2

imports org/sugarj/sxbld/Environment


strategies
	/**
	 * Prefixes all sort names with the base language name
	 *
	 * @type a -> a
	 */
	sxbld-prefix-all-sorts-base-lang = topdown(try(sxbld-prefix-sort-base-lang))

    /**
     * Prefixes all sort names with the given prefix
     *
     * @param prefix String
     *
     * @type a -> a
     */
	sxbld-prefix-all-sorts(|prefix) = topdown(try(sxbld-prefix-sort(|prefix)))
	
	/**
	 * Prefixes the sort name with the base language name
	 *
	 * @type Sdf2Symbol -> Sdf2Symbol
	 */
	sxbld-prefix-sort-base-lang =
		where(<sxbld-env-get-base-language-name> () => prefix);
		sxbld-prefix-sort(|prefix)

	/**
	 * Prefixes string with the base language name.
	 *
	 * @type String -> String
	 */
	sxbld-prefix-string-base-lang =
		?string;
		where(<sxbld-env-get-base-language-name> () => prefix);
		<conc-strings> (prefix, string)

    /**
     * Prefixes the sort name with the given prefix
     *
     * @param prefix String
     *
     * @type Sdf2Symbol -> Sdf2Symbol
     */
	sxbld-prefix-sort(|prefix) =
		let conc-prefix = ?orig-name; <conc-strings> (prefix, orig-name)
		in
			sort(conc-prefix) +
			parameterized-sort(conc-prefix, id)
		end

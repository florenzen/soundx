/*
 * Copyright (c) 2015, TU Berlin
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are
 * met:
 *
 * - Redistributions of source code must retain the above copyright
 *   notice, this list of conditions and the following disclaimer.
 * - Redistributions in binary form must reproduce the above
 *   copyright notice, this list of conditions and the following
 *   disclaimer in the documentation and/or other materials provided
 *   with the distribution.
 * - Neither the name of the TU Berlin nor the names of its
 *   contributors may be used to endorse or promote products derived
 *   from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

/**
 * Classification of inference rules
 *
 * @author Florian Lorenzen <florian.lorenzen@tu-berlin.de>
 */
module org/sugarj/soundx/Classification

imports org/sugarj/soundx/AbstractSyntax
		org/sugarj/soundx/BasicDesugaring
		org/sugarj/soundx/DepthFirstBC
		org/sugarj/soundx/Declarations
		org/sugarj/soundx/AnalysisFunctions
		org/sugarj/soundx/Environment
		org/sugarj/soundx/PrettyPrint
		org/sugarj/soundx/InductiveDefinitions
		org/sugarj/soundx/processing/Prefixing

imports org/sugarj/soundx/Debug


strategies
	/**
	 * Classifies all inference rules. It fails if a rule cannot be classified.
	 *
	 * @param current-ext String
	 * @param base-inf-rules List(SXInfRule)
	 *
	 * @type List(SXInfRule) -> _
	 */
	sx-classify-all-inf-rules(|current-ext, base-inf-rules) =
		?ext-inf-rules;
		map(sx-classify-inf-rule(|current-ext, ext-inf-rules, base-inf-rules))
		
	/**
	 * Classifies an inference rule. It fails if the inference rule cannot
	 * be classified.
	 *
	 * @param current-ext String
	 * @param ext-inf-rules List(SXInfRule)
	 * @param base-inf-rules List(SXInfRule)
	 *
	 * @type SXInfRule -> SXClassification
	 */
	sx-classify-inf-rule(|current-ext, ext-inf-rules, base-inf-rules) =
		?inf-rule@(premises, name, _);
		<sx-classify-inf-rule-x(|current-ext, ext-inf-rules, base-inf-rules)> inf-rule => x-res;
		if !x-res; ?SXRuleX() then
			!SXRuleX()
		else
			<sx-classify-inf-rule-b(|current-ext, ext-inf-rules, base-inf-rules)> inf-rule => b-res;
			if !b-res; ?SXRuleB() then
				!SXRuleB()
			else
				// make error message
				<sx-env-get-named-inf-rule> name => Some(input@SXNamedInferenceRule(orig-name, _));
				<sx-basic-desugar-inf-rule(|current-ext)> inf-rule => des-rule@SXInfRule(des-premises, _, des-concl);
				<sx-pretty-print-term> des-concl => des-concl-str;
				<sx-pretty-print-list-comma> premises => premises-str;
				<sx-pretty-print-list-comma> des-premises => des-premises-str;
				<sx-classify-x-err-message> (premises-str, des-concl-str, x-res) => x-err-msg;
				<sx-classify-b-err-message> (des-premises-str, des-concl-str, b-res) => b-err-msg;
				if <eq> (x-res, b-res) then
					<conc-strings> (
						"Could not verify rule ", orig-name, ":\n  ",
						x-err-msg) => msg						
				else
					<conc-strings> (
						"Could not verify rule ", orig-name, "\n",
						"It is not an X rule:\n  ", x-err-msg, "\n",
						"It is not a B rule:\n  ", b-err-msg) => msg
				end;
				<sx-analysis-fail(|msg)> input
			end
		end
		// ((sx-classify-inf-rule-x(|current-ext, ext-inf-rules, base-inf-rules) +
		//   sx-classify-inf-rule-b(|current-ext, ext-inf-rules, base-inf-rules)) <+
		//  <sx-env-get-named-inf-rule> name => Some(input);
		//  <sx-analysis-fail(|"Could not verify this rule")> input)
		//  // TODO give more helpful error message including assumptions and desugared conclusion

	/**
	 * Makes an error message why a rule is not an X rule.
	 *
	 * @type (String, String, SXClassification) -> String
	 */
	sx-classify-x-err-message =
		?(premises-str, concl-str, SXClassNoDeriv());
		<conc-strings> (
			"Cannot derive ", concl-str, " from ", premises-str
		)
	sx-classify-x-err-message =
		?(premises-str, concl-str, SXClassNotBase(name));
		<sx-unprefix-name> name => name-orig;
		<conc-strings> (
			"The last rule (", name-orig, ") in the derivation of ",
			concl-str, " is from the extension (forgotten desugaring?)"
		)
	sx-classify-x-err-message =
		?(premises-str, concl-str, SXClassConclAsm());
		<conc-strings> (
			"The desugared conclusion ", concl-str, " must not be in the premises"
		)

	/**
	 * Makes an error message why a rule is not a B rule.
	 *
	 * @type (String, String, SXClassification) -> String
	 */
	sx-classify-b-err-message =
		?(premises-str, concl-str, SXClassNoDeriv());
		<conc-strings> (
			"Cannot derive ", concl-str, " from ", premises-str
		)
	sx-classify-b-err-message =
		?(premises-str, concl-str, SXClassExtPrem());
		<conc-strings> (
			"The desugared premises ", premises-str, " contain extended expressions"
		)
		
	/**
	 * Tries to classify a rule as an X rule or returns a reason why this
	 * is not possible.
	 *
	 * @param current-ext String
	 * @param ext-inf-rules List(SXInfRule)
	 * @param base-inf-rules List(SXInfRule)
	 *
	 * @type SXInfRule -> SXClassification
	 */
	sx-classify-inf-rule-x(|current-ext, ext-inf-rules, base-inf-rules) =
		?rule@SXInfRule(premises, name, concl);
		// <sx-debug(|"trying to verify as X: ")> (<sx-pretty-print-term> rule);
		<sx-basic-desugar-inf-rule(|current-ext)> rule => des-rule@SXInfRule(des-premises, _, des-concl);
	    // <sx-debug(|"desugared rule ")> des-rule;
		<map(!SXDerivAsm(<id>))> premises => asms;
		<conc> (ext-inf-rules, base-inf-rules) => all-inf-rules;
		if <sx-dfsbc-build-derivation> (asms, all-inf-rules, des-concl) => deriv then
			if !deriv; ?SXDerivInst(_, rule-name, _) then
				<map(sx-inf-rule-name)> base-inf-rules => base-rule-names;
				if <elem> (rule-name, base-rule-names) then
					// <sx-debug(|"successfully classified as X: ")> name;
					!SXRuleX()
				else
					!SXClassNotBase(rule-name)
					//; <sx-debug(|"failed to classify as X rule (last rule not from base): ")> name
				end
			else
				!SXClassConclAsm()
				//; <sx-debug(|"failed to classify as X rule (conclusion in assumptions): ")> name
			end
		else
			!SXClassNoDeriv()
			//; <sx-debug(|"failed to classify as X rule (no derivation): ")> name
		end

	/**
     * Tries to classify a rule as a B rule or returns a reason why this
	 * is not possible.
	 *
	 * @param current-ext String
	 * @param ext-inf-rules List(SXInfRule)
	 * @param base-inf-rules List(SXInfRule)
	 *
	 * @type SXInfRule -> SXClassification
	 */
	sx-classify-inf-rule-b(|current-ext, ext-inf-rules, base-inf-rules) =
		?rule@SXInfRule(premises, name, concl);
		// <sx-debug(|"trying to verify as B: ")> (<sx-pretty-print-term> rule);
		<sx-basic-desugar-inf-rule(|current-ext)> rule => des-rule@SXInfRule(des-premises, _, des-concl);
		// <sx-debug(|"desugared rule ")> des-rule;
		<sx-extension-cons-names> current-ext => ext-cons-names;
		<sx-collect-all-cons-names> des-premises => des-premises-cons-names;
		// <sx-debug(|"all cons names of the premises ")> des-premises-cons-names;
		if <isect> (ext-cons-names, des-premises-cons-names); ?[] then
			<map(!SXDerivAsm(<id>))> des-premises => asms;
			if <sx-dfsbc-build-derivation> (asms, base-inf-rules, des-concl) => deriv then
				// <sx-debug(|"successfully classified as B: ")> name;
				!SXRuleB()
			else
				!SXClassNoDeriv()
				// <sx-debug(|"failed to classify as B rule (no derivation): ")> name;
			end
		else
			!SXClassExtPrem()
			// <sx-debug(|"failed to classify as B rule (extended expressions in premises): ")> name;
		end

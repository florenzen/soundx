module org/sugarj/soundx/processing/Desugarings

imports org/sugarj/soundx/AbstractSyntax
		org/sugarj/soundx/Environment
		org/sugarj/soundx/ExtensionDefinition
		org/sugarj/soundx/AnalysisFunctions

imports org/sugarj/soundx/Debug


strategies
	sx-process-desugarings =
		?SXDesugarings(_);
		where(
			if sx-env-is-extension-processing then
				SXDesugarings(map(sx-process-desugaring))
			else
				sx-analysis-fail(|"desugarings must be inside an extension")
			end)

	sx-process-desugaring =
		sx-process-guarded-desugaring +
		sx-process-universal-desugaring

	sx-process-universal-desugaring =
		?SXUniversalDesugaring(lhs, _, rhs);
		!SXUnivDesug(lhs, rhs); sx-meta-var-to-sx-metavar => desugaring;
		<sx-env-add-desugaring> desugaring;
		<sx-debug(|"added universal desugaring ")> desugaring

	sx-process-guarded-desugaring =
		?con#(args);
		//<debug(!"ARGS ")> args;
		//<debug(!"CON ")> con;
		<sx-process-split-guarded-des-cons> con => (index, judg-name);
		//debug(!"RECOGNICED GUARDED DES ");
		<length> args => len;
		//<debug(!"LENGTH ")> len;
		<index> (1, args) => premises;
		<collect-all(sx-is-judgement, conc)> premises => premises-list;
		//<debug(!"PREMISES ")> premises;
		<index> (len, args) => rhs;
		<drop(|2); take(|<subt> (len, 4))> args => concl-args;
		<split-at(|<subt> (index, 1))> concl-args => (exprs-before, [expr-subject|exprs-after]);
		//<debug(!"EXPRS BEFORE")> exprs-before;
		!SXGuardDesug(premises-list, exprs-before, expr-subject,
			exprs-after, judg-name, rhs); sx-meta-var-to-sx-metavar => desugaring;
		<sx-env-add-desugaring> desugaring;
		<sx-debug(|"added guarded desugaring ")> desugaring

	sx-process-split-guarded-des-cons =
		?con;
		<string-starts-with(|"SXGuardedDesugaring")> con;
		//debug(!"STRING STARTS WITH");
		<explode-string> con => chars;
		//debug(!"CHARS ");
		<drop(|19)> chars => chars-no-prefix;
		<take-while(is-num); implode-string; string-to-int> chars-no-prefix => index;
		//<debug(!"INDEX ")> index;
		<drop-while(is-num); implode-string> chars-no-prefix => judg-name;
		!(index, judg-name)

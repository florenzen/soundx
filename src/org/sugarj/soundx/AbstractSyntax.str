/*
 * Copyright (c) 2015, TU Berlin
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are
 * met:
 *
 * - Redistributions of source code must retain the above copyright
 *   notice, this list of conditions and the following disclaimer.
 * - Redistributions in binary form must reproduce the above
 *   copyright notice, this list of conditions and the following
 *   disclaimer in the documentation and/or other materials provided
 *   with the distribution.
 * - Neither the name of the TU Berlin nor the names of its
 *   contributors may be used to endorse or promote products derived
 *   from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

/**
 * SoundX abstract syntax
 *
 * @author Florian Lorenzen <florian.lorenzen@tu-berlin.de>
 */
module org/sugarj/soundx/AbstractSyntax

imports org/sugarj/soundx/MetaExplode

imports org/sugarj/soundx/Substitution


/**
 * Metavariables
 */
signature constructors
	SXMetaVar:    String       -> SXMetaVar
	SXMetaVarGen: String * Int -> SXMetaVar

strategies
	/**
	 * Translates meta-var to SXMetaVar
	 *
	 * @type a -> a
	 */
    sx-meta-var-to-sx-metavar =
    	topdown(try(\meta-var(name) -> SXMetaVar(name)\))

	/**
	 * Checks if term is a metavariable
	 *
	 * @type a -> a
	 */
	sx-is-metavar =
		?SXMetaVar(_) +
		?SXMetaVarGen(_, _)

	/**
	 * Collects all metavariables of an expression
	 *
	 * @type a -> List(SXMetaVar)
	 */
    sx-vars =
    	collect-all(sx-is-metavar)

	/**
	 * Returns the name of a metavariable
	 *
	 * @type SXMetaVar -> String
	 */
	sx-metavar-name:
		SXMetaVar(name) -> name
	sx-metavar-name:
		SXMetaVarGen(name, num) -> <conc-strings> (name, <int-to-string> num)

	/**
	 * Returns a fresh variant of a meta variable
	 *
	 * The result is always an SXMetaVarGen(name, num).
	 *
	 * @type SXMetaVar -> SXMetaVar
	 */
	sx-fresh-metavar =
		?SXMetaVar(name);
		<newname> ""; string-to-int => num;
		!SXMetaVarGen(name, num)
	sx-fresh-metavar =
		?SXMetaVarGen(name, _);
		<newname> ""; string-to-int => num;
		!SXMetaVarGen(name, num)

/**
 * Inference rules
 */
signature constructors
	SXInfRule: List(SXJudgement) * String * SXJudgement -> SXAbsInfRule
	
	SXRuleX: SXClassification
	SXRuleB: SXClassification

strategies
	/**
	 * Returns a fresh variant of an inference rule
	 *
	 * @type SXInfRule -> SXInfRule
	 */
	sx-fresh-inf-rule =
		?inf-rule;
		let fresh =
			{ expr, sub, var, sub1:
				?(expr, sub);
				<sx-is-metavar> expr;
				if <sx-sub-defines> (sub, expr) then
					!(<sx-sub-apply> (sub, expr), sub)
				else
					<sx-fresh-metavar> expr => var;
					<sx-sub-compose> (<sx-sub-singleton> (expr, var), sub) => sub1;
					!(var, sub1)
				end
			}
		in
			sx-sub-empty => sub-empty;
			<thread-bottomup(try(fresh))> (inf-rule, sub-empty) => (rule-fresh, _);
			!rule-fresh
		end

	/**
	 * Returns the conclusions of an inference rule.
	 *
	 * @type SXInfRule -> SXJudgement
	 */
	sx-concl =
		?SXInfRule(_, _, concl); !concl

	/**
	 * Returns the name of an inference rule
	 *
	 * @type SXInfRule -> String
	 */
	sx-inf-rule-name =
		?SXInfRule(_, name, _); !name

	/**
	 * Returns the premises of an inference rule
	 *
	 * @type SXInfRule -> List(SXJudgement)
	 */
	sx-inf-rule-premises =
		?SXInfRule(premises, _, _); !premises

	/**
	 * Axiom schema for SXNeq judgement. It is only used in
	 * desugaring.
	 *
	 * @type _ -> SXInfRule
	 */
	sx-prim-axiom-neq =
		!SXInfRule([], "=/=", SXNeq(SXMetaVar("x"), SXMetaVar("y")))
	
	/**
	 * Axiom schema for SXGetInterface judgement. It is only used in
	 * desugaring.
	 *
	 * @type _ -> SXInfRule
	 */
	sx-prim-axiom-interface =
		!SXInfRule([], "=interface()", SXGetInterface(SXMetaVar("x"), SXMetaVar("y")))

/**
 * Judgements
 */
signature constructors
    SXNeq:          SXExpr * SXExpr -> SXJudgement
    SXGetInterface: SXExpr * SXExpr -> SXJudgement

strategies
	/**
	 * Checks if an aterm is a judgement
	 *
	 * A judgement has a constructor named SXJudgement<number> or
	 * is one of the primitive judgement SXNeq, SXGetInterface.
	 *
	 * @type a -> a
	 */
	sx-is-judgement =
		where(sx-is-ind-def-judgement) +
		sx-is-prim-judgement

	/**
	 * Checks if an aterm is an inductively defined judgement.
	 *
	 * It returns the name of the judgement and ist arguments.
	 *
	 * @type a -> (String, List(SXExpr))
	 */
	sx-is-ind-def-judgement =
		?c#(args);
		<string-starts-with(|"SXJudgement")> c;
		!(c, args)

	/**
	 * Checks if aterm is a primitive judgement
	 *
	 * @type a -> a
	 */
    sx-is-prim-judgement =
    	sx-is-neq-judgement + sx-is-get-interface-judgement

	/**
	 * Checks if aterm is a "Get Interface" judgement
	 *
	 * @type a -> a
	 */
	sx-is-get-interface-judgement =
		?SXGetInterface(_, _)

	/**
	 * Checks if aterm is an SXEq judgement
	 *
	 * @type a -> a
	 */
	sx-is-neq-judgement =
		?SXNeq(_, _)

/**
 * Desugarings
 */
signature constructors
	SXGuardDesug: List(SXJudgement) * List(SXExpr) * SXExpr * List(SXExpr) * String * SXExpr -> SXDesug
	SXUnivDesug:  SXExpr * SXExpr                                                            -> SXDesug

strategies
	sx-is-universal-desugaring =
		?SXUnivDesug(_, _)

/**
 * Toplevel declaration list
 */
signature constructors
	SXToplevelDeclarationsCons: SXToplevelDeclaration * SXToplevelDeclarations -> SXToplevelDeclarations
	SXToplevelDeclarationsNil:                                                    SXToplevelDeclarations

strategies
	/**
	 * Converts list of toplevel declarations into ToplevelDeclarations
	 *
	 * @type List(SXToplevelDeclaration) -> SXToplevelDeclarations
	 */
	sx-list-to-toplevel-decls:
		[] -> SXToplevelDeclarationsNil()
	sx-list-to-toplevel-decls:
		[decl|decls] -> SXToplevelDeclarationsCons(decl, decls1)
		where
			<sx-list-to-toplevel-decls> decls => decls1

	/**
	 * Converts ToplevelDeclarations into list of toplevel declarations
	 *
	 * @type SXToplevelDeclarations -> List(SXToplevelDeclaration)
	 */
	sx-toplevel-decls-to-list:
		SXToplevelDeclarationsNil() -> []
	sx-toplevel-decls-to-list:
		SXToplevelDeclarationsCons(decl, decls) -> [decl|decls1]
		where
			<sx-toplevel-decls-to-list> decls => decls1

/**
 * Derivations
 */
signature constructors
	SXDerivInst: List(SXDeriv) * String * SXJudgement -> SXDeriv
	SXDerivAsm:  SXJudgement                          -> SXDeriv

strategies
	/**
	 * Extracts the conclusion from a derivation
	 *
	 * @type SXDeriv -> SXJudgement
	 */
	sx-concl:
		SXDerivInst(_, _, concl) -> concl
	sx-concl:
		SXDerivAsm(concl) -> concl

/**
 * Various
 */
signature constructors
	SXUnificationFailure: String -> SXUnificationResult

	SXFalse: SXBool
	SXTrue:  SXBool

strategies
	/**
	 * Collects all constructor names of some term
	 *
	 * @type a -> List(String)
	 */
 	sx-collect-all-cons-names =
 		let collect-s =
 			{ term, con, args:
 				?term@con#(args);
 				if sx-is-judgement + sx-is-metavar + is-int + is-string +
 					is-list + is-tuple + is-real then
 					fail
 				else
 					!con
 				end
 			}
 		in
 			collect-all(collect-s)
		end
